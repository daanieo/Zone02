/**
* Name: Zone02_simulation
* Based on the internal empty template. 
* Author: daan
* Tags: 
*/


model Zone02_simulation

import "facilities_model.gaml"


global {
//	System specifications
	file shape_file_buildings <- shape_file("/home/daan/GAMA/workspace/Zone02/files/rr_within_zone02.shp","EPSG:4326");
	file faccoordinates <- shape_file("/home/daan/Desktop/gdf.shp","EPSG:4326");
	geometry shape <-envelope(shape_file_buildings);
	
//	Time-related constants
	float step <- 10 #minutes;
	int cycles_in_day <- 24*6; 
	
//	Constants as parameters
	int served_parallel <- 10 parameter: "Est. facility capacity per cycle"; // Beneficiaries that can be served per cycle
	int avg_nb_building <- 2 parameter: "Est. avg beneficiaries per building"; 	// the average number of beneficiaries per building 
		
//	Constants as policy options
	int rerouting <- 5;
	
//	Statistics
	float total_food_delivered <- 0.0;
	float total_food_demanded <- 0.0;
	int total_hungry_days <- 0; 
	
	list<int> hungry_days_list <- [];
	
	
	init {

//		Create facility agents
		create facilities from: faccoordinates with: [facility_food_storage_size::read('Size')] { 
							
			// Assign empties to variables 
			nb_beneficiaries <- 0; 
			queue <- [];
			queue_open<-true;
			facility_food_storage <- facility_food_storage_size * avg_nb_building; // At initialisation, food storage has max capacity 
			

		}
		
//		Create household agents
		create households from: shape_file_buildings{
			
			speed <- 4 #km/#hour;					// speed of beneficiaries 
	
			// Constants
			food_consumption <- 0.6; 				// kg rice / pers / day
			home_location <- location;				// home location = current location 
			my_facility <- determine_facility();	// home facility is current closest facility 
			nb_members <- rnd(1,(avg_nb_building*2-1)+1); 				// 1 to 5 persons in one household 
			nb_days_tolerated <- rnd(1,10);			// days of food tolerated for perceived need 
			
			
			// Initial values variables
			hungry_days <- 0;					// hungry days of beneficiary agent 
			food_storage <- rnd(0,50);			// initial food storage 
			facility_of_choice <- my_facility;	// initally facility of choice is my facility 
			
			// add the number of household members to the facility			
			ask my_facility {
				nb_beneficiaries <- nb_beneficiaries + myself.nb_members; 
			}			
		} 
	}
	
	reflex collect_stats {		
		add total_hungry_days to: hungry_days_list;
	}
}





experiment simple_simulation type: gui {
//	parameter "Shapefile for the buildings:" var: shape_file_buildings category: "GIS" ;
 	parameter "Est. facility capacity per cycle" var: served_parallel min: 1 max:100 step: 1;
	parameter "Est. avg beneficiaries per building" var: avg_nb_building min: 1 max:5 step: 0.5;
	
		
	output {
		display zone_display{
			species households aspect: map_visualisation;
			species facilities aspect:map_visualisation;
		}
		

		
	display data_viz {
       	chart "Average hungry days " type: series y_range:[0,4000] x_range: [cycle-25,cycle+25] size: {1,0.5} position: {0, 0}{
        data "global" value: ( total_hungry_days / ((cycle+1)/24) ); // cycle + 1, otherwise illegal division by zero
    }

       	chart "Facilities food storage" type: histogram y_label: "kg rice" y_range:[0,4000*avg_nb_building] x_range: [cycle-25,cycle+25] size: {0.5,0.5} position: {0, 0.5}{
        loop f over: facilities {
        	data f.name value: f.facility_food_storage;
        }

    }    
       	chart "Facilities queue lengths" type: histogram y_label: "# people" y_range:[0,400] x_range: [cycle-25,cycle+25]size: {0.5,0.5} position: {0.5, 0.5} {
        loop f over: facilities {
    		data f.name value: length(f.queue);
		}

    }	  
	  
	}
	monitor "Total food delivery gap" value: total_food_demanded-total_food_delivered;
	monitor "Total hungry days" value: total_hungry_days;
	}
}

// Make sure to have deleted the file result.txt in the model dir before running this experiment
// otherwise data is appended to data generated by the previous run. 
experiment batch_experiment type: batch repeat: 4 keep_seed: true until: ( cycle > 10*24*6 ) {

	parameter "Est. facility capacity per cycle" var: served_parallel min: 10 max: 100 step: 10;
	parameter "Est. avg beneficiaries per building" var: avg_nb_building min: 0 max: 5 step: 1;

	action save_results {				
		loop i over: simulations{			
        	write int(self);
        	save [i.name, served_parallel,avg_nb_building, i.hungry_days_list] to: "result.txt" type: "csv" rewrite: false;
	}
}
	
	reflex save_results_explo {		
		do save_results;
	}
}












